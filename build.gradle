plugins {
	id 'fabric-loom' version '0.2.7-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '5.2.0'
}

repositories {
	maven { url 'https://jitpack.io' }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.minecraft_version+'-'+project.mod_version
group = project.maven_group

configurations {
	shade
	implementation.extendsFrom shade
}

def shadeExclusions = {
}

shadowJar {
	from project.configurations.shade
	configurations = [
		project.configurations.shade
	]
	exclude '.files.swp'
	exclude 'mojang-translations'
	exclude 'com/google/**'

	classifier = 'all-unremapped'

	relocate 'org.apache.commons', 'com.hadroncfy.libs.apache'
	relocate 'com.replaymod.replaystudio', 'com.hadroncfy.libs.replaystudio'
	relocate 'net.md_5.bungee', 'com.hadroncfy.libs.bungee'
	relocate 'com.github.steveice10', 'com.hadroncfy.libs.steveice10'
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	// modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.

	shade "com.github.ReplayMod.ReplayStudio:1.14.4:be6a680", shadeExclusions
	compileOnly "com.google.code.gson:gson:2.8.6"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

build.dependsOn shadowJar

task remapShadowJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: shadowJar) {
	input = tasks.shadowJar.archiveFile
	classifier = 'all'
	addNestedDependencies.set(true)
}

defaultTasks 'remapShadowJar'